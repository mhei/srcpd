dnl configure.in for srcpd
dnl Revision: $Revision$
dnl Last update: $Date$

dnl Process this file with autoconf to produce a configure script.

AC_INIT([srcpd], [2.0.8], [Matthias Trute <mtrute@users.sourceforge.net>])
AC_CANONICAL_TARGET
AC_CONFIG_HEADER([config.h])
AC_CONFIG_SRCDIR([srcpd.c])
AM_INIT_AUTOMAKE([dist-bzip2])

AC_PREFIX_DEFAULT(/usr/local)

dnl this is from scmxx
AH_TEMPLATE(
    [NO_CFMAKERAW],
    [define this to include own cfmakeraw()]
)
AC_CHECK_FUNC(
    cfmakeraw,
    [],
    [AC_DEFINE(NO_CFMAKERAW)]
)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

dnl Checks for host dependend libraries.
case $host in
  *-*-linux*)
    ;;
  *-*-cygwin*)
    AC_CHECK_LIB(ioperm, ioperm, , AC_MSG_ERROR(lib ioperm is missing))
    ;;
  *-*-freebsd*)
    ;;
  *)
    AC_MSG_WARN(System $host not known.)
    ;;
esac


dnl Checks for header files.
dnl TODO: fix double check of stdlib.h, string.h, strings.h unistd.h
AC_CHECK_HEADERS(fcntl.h stdlib.h string.h strings.h syslog.h unistd.h \
         errno.h pthread.h pwd.h sys/time.h signal.h termios.h)

dnl Checks for host dependend header files.
case $host in
  *-*-linux*)
    AC_CHECK_HEADERS(sys/io.h linux/serial.h linux/lp.h)
    ;;
  *-*-cygwin*)
    AC_CHECK_HEADERS(sys/io.h)
    ;;
  *-*-freebsd*)
    AC_CHECK_HEADERS(dev/ppbus/ppi.h)
    ;;
  *)
    ;;
esac

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

AC_MSG_CHECKING([for type of build])
AC_ARG_ENABLE(debug,
        AC_HELP_STRING([--enable-debug], [enable debug mode [default=no]]),
 enable_debug=$enableval, enable_debug=no)
if test $enable_debug = yes; then
   CFLAGS="-g -Wall"
   AC_MSG_RESULT(debug)
else
   CFLAGS="-O2 -Wall"
   AC_MSG_RESULT(release)
fi

dnl Checks for library functions.
AC_FUNC_MEMCMP
AM_PATH_XML2([2.4.0], [HAVE_XML2=yes], [exit 1])

dnl Checks for host dependend library functions.
case $host in
  *-*-linux*)
    AC_CHECK_FUNC(ioperm, , AC_MSG_ERROR(support missing))
    ;;
  *-*-cygwin*)
    AC_CHECK_FUNC(ioperm, , AC_MSG_ERROR(support missing))
    ;;
  *-*-freebsd*)
    ;;
  *)
    ;;
esac

CFLAGS="${CFLAGS} ${XML_CFLAGS}"
LIBS="${LIBS} ${XML_LIBS}"

AC_TYPE_PID_T
AC_HEADER_TIME
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(gettimeofday socket)

AC_MSG_CHECKING([whether to enable ipv6])
AC_ARG_ENABLE(ipv6,
        AC_HELP_STRING([--enable-ipv6], [enable IPv6 extensions]),
[ case "$enableval" in
        no)
                AC_MSG_RESULT(no)
                ipv6=no
                ;;
        *)
                AC_MSG_RESULT(yes)
                AC_DEFINE(ENABLE_IPV6, [1], [Activate IP V6])
                ipv6=yes
                ;;
  esac ],
AC_TRY_RUN([ /* AF_INET6 available check */
#include <sys/types.h>
#include <sys/socket.h>
main()
{
 if (socket(AF_INET6, SOCK_STREAM, 0) < 0)
   exit(1);
 else
   exit(0);
}
],
  AC_MSG_RESULT(yes)
  AC_DEFINE(ENABLE_IPV6, [1], [Activate IP V6])
  ipv6=yes,
  AC_MSG_RESULT(no)
  ipv6=no,
  AC_MSG_RESULT(no)
  ipv6=no
))

AC_OUTPUT(Makefile)
